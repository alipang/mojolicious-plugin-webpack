package Mojolicious::Plugin::Webpack;
use Mojo::Base 'Mojolicious::Plugin';

use Mojo::ByteStream 'b';
use Mojo::File 'path';
use Mojo::IOLoop::Subprocess;
use Mojo::JSON;

our $VERSION = '0.01';

sub assets_dir { $_[0]->{assets_dir} }
sub daemon     { $_[0]->{daemon} }

has dependencies => sub {
  return {
    core => [qw(webpack-cli webpack webpack-md5-hash clean-webpack-plugin html-webpack-plugin)],
    css  => [qw(css-loader mini-css-extract-plugin optimize-css-assets-webpack-plugin)],
    js   => [qw(@babel/core @babel/preset-env babel-loader uglifyjs-webpack-plugin)],
    sass => [qw(node-sass sass-loader)],
    vue  => [qw(vue vue-loader vue-template-compiler)],
  };
};

sub env     { $_[0]->{env} }
sub out_dir { $_[0]->{out_dir} }
sub process { [@{$_[0]->{process} || []}] }

sub register {
  my ($self, $app, $config) = @_;

  $self->{$_} = path $config->{$_} for grep { $config->{$_} } qw(assets_dir out_dir);
  $self->{assets_dir} ||= path $app->home->rel_file('assets');
  $self->{env} = $config->{env} // $app->mode;
  $self->{out_dir} ||= path $app->static->paths->[0], 'asset';
  $self->{process} = $config->{process} || ['js'];
  $self->{source_maps} = $config->{source_maps} // 1;

  $self->dependencies->{$_} = $config->{dependencies}{$_} for keys %{$config->{dependencies} || {}};
  $self->_run_webpack($app, $config) if $ENV{MOJO_WEBPACK_ARGS} // 1;
  $app->helper(asset => sub { _asset($self, @_) });
}

sub _asset {
  my ($self, $c, @args) = @_;
  return $self unless @args;
  return b '<script>/* $c->asset("TODO") */</script>';
}

sub _generate {
  my ($self, $app, $dir, $name) = @_;
  my $file = path $dir, $name;
  my $is_generated = '';

  eval {
    my $CFG = $file->open('<');
    /Autogenerated\s*by\s*Mojolicious-Plugin-Webpack/i and $is_generated = $_ while <$CFG>;
  };

  return 'custom' if !$is_generated and -r $file;
  return 'current' if $is_generated =~ /\b$VERSION\b/;

  my $template = $self->_share_dir->child($name)->slurp;
  $template =~ s!__AUTOGENERATED__!Autogenerated by Mojolicious-Plugin-Webpack $VERSION!g;
  $template =~ s!__NAME__!{$app->moniker}!ge;
  $template =~ s!__VERSION__!{$app->VERSION || '0.0.1'}!ge;
  $file->spurt($template);
  return 'generated';
}

sub _environment_variables {
  my $self = shift;
  my %env  = %ENV;

  $env{NODE_ENV} ||= $self->env;
  $env{WEBPACK_ASSETS_DIR} = $self->assets_dir;
  $env{WEBPACK_OUT_DIR}    = $self->out_dir;
  $env{WEBPACK_SHARE_DIR}    //= $self->_share_dir;
  $env{WEBPACK_AUTO_CLEANUP} //= $self->{auto_cleanup} // 1;
  $env{WEBPACK_SOURCE_MAPS}  //= $self->{source_maps} // 1;
  $env{uc "WEBPACK_RULE_FOR_$_"} = 1 for @{$self->process};

  return \%env;
}

sub _generate_custom_webpack_hook {
  my $self = shift;
  my $file = $self->assets_dir->child('webpack-custom.js');

  return -r $file ? $file : $file->spurt(<<"HERE");
// assetsDir = "@{[$self->assets_dir]}"
module.exports = function(config) {
  config.entry = {
    "my_app": config.assetsDir + '/js/app.js',
  };
};
HERE
}

sub _install_node_deps {
  my ($self, $dir) = @_;
  my $package_json = Mojo::JSON::decode_json(path($dir, 'package.json')->slurp);
  my $n = 0;

  system qw(npm install) if %{$package_json->{dependencies}} and !-d path $dir, 'node_modules';

  for my $preset ('core', @{$self->process}) {
    for my $module (@{$self->dependencies->{$preset} || []}) {
      next if $package_json->{dependencies}{$module};
      warn "[Webpack] npm install $module\n" if $ENV{MOJO_WEBPACK_DEBUG};
      system npm => install => $module;
      $n++;
    }
  }

  return $n;
}

sub _run_webpack {
  my ($self, $app, $config) = @_;
  my $config_file = path $config->{config_file} || $app->home->rel_file('webpack.config.js');
  my $env = $self->_environment_variables;

  $self->_generate($app, $config_file->dirname, 'package.json');
  $self->_generate($app, $config_file->dirname, 'webpack.config.js');
  $self->_generate_custom_webpack_hook;
  $self->_install_node_deps($config_file->dirname);

  unless (-e $env->{WEBPACK_OUT_DIR}) {
    path($env->{WEBPACK_OUT_DIR})->make_path;
  }
  unless (-w $env->{WEBPACK_OUT_DIR}) {
    warn "[Webpack] Cannot write to $env->{WEBPACK_OUT_DIR}\n" if $ENV{MOJO_WEBPACK_DEBUG};
    return;
  }

  my @cmd = $config->{webpack} || $app->home->rel_file('node_modules/.bin/webpack');
  push @cmd, '--config' => $config_file->to_string;
  push @cmd, '--env'    => $self->env;
  push @cmd, '--cache',   '--progress' if $self->env eq 'development';
  push @cmd, '--profile', '--verbose'  if $ENV{MOJO_WEBPACK_VERBOSE};
  push @cmd, split /\s+/, +($ENV{MOJO_WEBPACK_ARGS} || '');

  warn "[Webpack] @cmd\n" if $ENV{MOJO_WEBPACK_DEBUG};
  map { warn "[Webpack] $_=$env->{$_}\n" } grep {/^WEBPACK_/} sort keys %$env if $ENV{MOJO_WEBPACK_DEBUG};
  local %ENV = %$env;
  return system @cmd unless grep {/--watch/} @cmd;
  $self->{daemon} = Mojo::IOLoop::Subprocess->new->run(sub { %ENV = %$env; system @cmd }, sub { });
}

sub _share_dir {
  state $share = path(path(__FILE__)->dirname, 'Webpack');
}

1;
